<string>            ::= <atom> (<union> | <branch> | <split>)*
<union>             ::= <bond>? ( <cut> | <string> )
<branch>            ::= "(" ( "." | <bond> )? <string> ")"
<split>             ::= "." <string>
<atom>              ::= <shortcut> | <selected-shortcut> | "*" | <bracket>
<bracket>           ::= "[" <isotope>?
                         ( <element> | <selected-element> | "*" )
                         <stereodescriptor>? <virtual-hydrogen>? <charge>?
                         <extension>? "]"
<isotope>           ::= <non-zero> <digit>? <digit>?
<virtual-hydrogen>  ::= "H" <digit>?
<charge>            ::= ( "+" | '-' ) <non-zero>?
<extension>         ::= ":" <hex>? <hex>? <hex>? <hex>
<cut>               ::= <digit> | "%" <non-zero> <digit>
<stereodescriptor>  ::= "@" "@"?
<shortcut>          ::= "B" "r"? | "C" "l"? | "N" | "O" | "P" | "S"
                      | "F" | "I" | "At" | "Ts"
<selected-shortcut> ::= "b" | "c" | "n" | "o" | "p" | "s"
<element>           ::= "A" ( "c" | "g" | "l" | "m" | "r" | "s" | "t" | "u" )
                      | "B" ( "a" | "e" | "h" | "i" | "k" | "r" )?
                      | "C" ( "a" | "d" | "e" | "f" | "l" | "m" | "n" | "o"
                      | "r" | "s" | "u" )?
                      | "D" ( "b" | "s" | "y" )
                      | "E" ( "r" | "s" | "u" )
                      | "F" ( "e" | "l" | "m" | "r" )?
                      | "G" ( "a" | "d" | "e" )
                      | "H" ( "e" | "f" | "g" | "o" | "s" )?
                      | "I" ( "n" | "r" )?
                      | "K" "r"?
                      | "L" ( "a" | "i" | "r" | "u" | "v" )
                      | "M" ( "c" | "g" | "n" | "o" | "t" )
                      | "N" ( "a" | "b" | "d" | "e" | "h" | "i" | "o" | "p" )?
                      | "O" ( "g" | "s" )?
                      | "P" ( "a" | "b" | "d" | "m" | "o" | "r" | "t" | "u" )?
                      | "R" ( "a" | "b" | "e" | "f" | "g" | "h" | "n" | "u" )
                      | "S" ( "b" | "c" | "e" | "g" | "i" | "m" | "n" | "r" )?
                      | "T" ( "a" | "b" | "c" | "e" | "h" | "i" | "l" | "m"
                      | "s" )
                      | "U" | "V" | "W" | "Xe" | "Y" "b"?
                      | "Z" ( "n" | "r" )
<selected-element>  ::= "b" | "c" | "n" | "o" | "p" | "s" "e"? | "as"
<bond>              ::= "-" | "=" | "#" | "$" | "/" | "\\"
<non-zero>          ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<digit>             ::= "0" | <non-zero>
<hex>               ::= <digit> | "a" | "b" | "c" | "d" | "e" | "f"