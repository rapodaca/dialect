<string>            ::= <sequence>?
<sequence>          ::= <atom> ( <union> | <branch> | <split> )*
<union>             ::= <bond>? ( <cut> | <sequence> )
<branch>            ::= "(" ( <detachment> | <bond> )? <sequence> ")"
<split>             ::= <detachment> <sequence>
<atom>              ::= <star> | <shortcut> | <selected_shortcut> | <bracket>
<bracket>           ::= "[" <isotope>? <symbol> <stereodescriptor>?
                         <virtual_hydrogen>? <charge>? <extension>? "]"
<isotope>           ::= <non_zero> <digit>? <digit>?
<symbol>            ::= <star> | <element> | <selected_element>
<virtual_hydrogen>  ::= "H" <non_zero>?
<charge>            ::= ( "+" | "-" ) <non_zero>?
<extension>         ::= ":" <digit>? <digit>? <digit>? <digit>
<cut>               ::= <digit> | "%" <non_zero> <digit>
<stereodescriptor>  ::= "@" "@"?
<star>              ::= "*"
<detachment>        ::= "."
<shortcut>          ::= "B" "r"? | "C" "l"? | "N" | "O" | "P" | "S"
                      | "F" | "I" | "A" "t" | "T" "s"
<selected_shortcut> ::= "b" | "c" | "n" | "o" | "p" | "s"
<element>           ::= "A" ( "c" | "g" | "l" | "m" | "r" | "s" | "t" | "u" )
                      | "B" ( "a" | "e" | "h" | "i" | "k" | "r" )?
                      | "C" ( "a" | "d" | "e" | "f" | "l" | "m" | "n" | "o"
                      | "r" | "s" | "u" )?
                      | "D" ( "b" | "s" | "y" )
                      | "E" ( "r" | "s" | "u" )
                      | "F" ( "e" | "l" | "m" | "r" )?
                      | "G" ( "a" | "d" | "e" )
                      | "H" ( "e" | "f" | "g" | "o" | "s" )?
                      | "I" ( "n" | "r" )?
                      | "K" "r"?
                      | "L" ( "a" | "i" | "r" | "u" | "v" )
                      | "M" ( "c" | "g" | "n" | "o" | "t" )
                      | "N" ( "a" | "b" | "d" | "e" | "h" | "i" | "o" | "p" )?
                      | "O" ( "g" | "s" )?
                      | "P" ( "a" | "b" | "d" | "m" | "o" | "r" | "t" | "u" )?
                      | "R" ( "a" | "b" | "e" | "f" | "g" | "h" | "n" | "u" )
                      | "S" ( "b" | "c" | "e" | "g" | "i" | "m" | "n" | "r" )?
                      | "T" ( "a" | "b" | "c" | "e" | "h" | "i" | "l" | "m"
                      | "s" )
                      | "U" | "V" | "W" | "Xe" | "Y" "b"?
                      | "Z" ( "n" | "r" )
<selected_element>  ::= "a" "s" | "b" | "c" | "n" | "o" | "p" | "s" "e"?
<bond>              ::= "-" | "=" | "#" | | "/" | "\\"
<non_zero>          ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<digit>             ::= "0" | <non_zero>
