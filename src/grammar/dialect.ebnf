<string>            ::= <sequence>?
<sequence>          ::= <atom> ( <union> | <branch> | <split> )*
<union>             ::= <bond>? ( <cut> | <sequence> )
<branch>            ::= "(" ( <detachment> | <bond> )? <sequence> ")"
<split>             ::= <detachment> <sequence>
<atom>              ::= <star> | <shortcut> | <selection> | <bracket>
<bracket>           ::= "[" <isotope>? <symbol> <stereodescriptor>?
                         <virtual_hydrogen>? <charge>? <extension>? "]"
<isotope>           ::= <digit> <digit>? <digit>?
<symbol>            ::= <star> | <element> | <selection>
<virtual_hydrogen>  ::= "H" <digit>?
<charge>            ::= ( "+" | "-" ) <digit>?
<extension>         ::= ":" <digit>? <digit>? <digit>? <digit>
<cut>               ::= <digit> | "%" <digit> <digit>
<stereodescriptor>  ::= "@" "@"?
<star>              ::= "*"
<detachment>        ::= "."
<shortcut>          ::= "B" "r"? | "C" "l"? | "N" | "O" | "P" | "S" | "F" | "I"
<selection>         ::= "b" | "c" | "n" | "o" | "p" | "s"
<element>           ::= "A" ( "c" | "g" | "l" | "m" | "r" | "s" | "t" | "u" )
                      | "B" ( "a" | "e" | "h" | "i" | "k" | "r" )?
                      | "C" ( "a" | "d" | "e" | "f" | "l" | "m" | "n" | "o"
                      | "r" | "s" | "u" )?
                      | "D" ( "b" | "s" | "y" )
                      | "E" ( "r" | "s" | "u" )
                      | "F" ( "e" | "l" | "m" | "r" )?
                      | "G" ( "a" | "d" | "e" )
                      | "H" ( "e" | "f" | "g" | "o" | "s" )?
                      | "I" ( "n" | "r" )?
                      | "K" "r"?
                      | "L" ( "a" | "i" | "r" | "u" | "v" )
                      | "M" ( "c" | "g" | "n" | "o" | "t" )
                      | "N" ( "a" | "b" | "d" | "e" | "h" | "i" | "o" | "p" )?
                      | "O" ( "g" | "s" )?
                      | "P" ( "a" | "b" | "d" | "m" | "o" | "r" | "t" | "u" )?
                      | "R" ( "a" | "b" | "e" | "f" | "g" | "h" | "n" | "u" )
                      | "S" ( "b" | "c" | "e" | "g" | "i" | "m" | "n" | "r" )?
                      | "T" ( "a" | "b" | "c" | "e" | "h" | "i" | "l" | "m"
                      | "s" )
                      | "U" | "V" | "W" | "Xe" | "Y" "b"?
                      | "Z" ( "n" | "r" )
<bond>              ::= "-" | "=" | "#" | | "/" | "\\"
<digit>             ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8"
                      | "9"
